buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

group 'me.codeboy.common'
version '1.3.2'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'


def siteUrl = 'https://github.com/androiddevelop/CommonBase'
def issueUrl = 'https://github.com/androiddevelop/CommonBase/issues'
def gitUrl = 'https://github.com/androiddevelop/CommonBase.git'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'base'
                description 'Java Common Base'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'codeboy'
                        name 'yuedong'
                        email 'androiddevelop@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'CommonBase'
        desc = 'Java通用类封装'
        issueTrackerUrl = issueUrl
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['java', 'common', 'base']
        publicDownloadNumbers = true
        publish = true
        version {
            name = "1.3.2"
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
            mavenCentralSync {
                sync = true
                user = properties.getProperty("bintray.oss.user")
                password = properties.getProperty("bintray.oss.password")
                close = '1'
            }
        }
    }
}